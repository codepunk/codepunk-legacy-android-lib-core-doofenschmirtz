import com.android.build.gradle.api.BaseVariant

/*
 * Copyright (C) 2018 Codepunk, LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka-android'

buildscript {
    addRepos(repositories)

    dependencies {
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${rootProject.versions.dokka}"
    }
}

android {
    compileSdkVersion rootProject.build_versions.target_sdk
    buildToolsVersion rootProject.build_versions.build_tools

    defaultConfig {
        minSdkVersion rootProject.build_versions.min_sdk
        targetSdkVersion rootProject.build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
        includeNonPublic = true
    }

    libraryVariants.all { variant ->
        /*
         * Generate a set of unique bundle keys that are accessible as BuildConfig constants.
         */
        makeKeys(
                variant,
                "LISTENER_SOURCE",
                "REQUEST_CODE"
        )
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
//    implementation rootProject.deps.android.material
    implementation "androidx.appcompat:appcompat:${rootProject.versions.androidx_appcompat}"
//    implementation rootProject.deps.androidx.legacy
    implementation "androidx.preference:preference:${rootProject.versions.androidx_legacy}"
    implementation "com.android.support:design:${rootProject.versions.android_support_design}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${rootProject.versions.kotlin}"

//    kapt rootProject.deps.kotlin.compiler

    testImplementation "junit:junit:${rootProject.versions.junit}"

    androidTestImplementation "com.android.support.test.espresso:espresso-core:${rootProject.versions.test_espresso_core}"
    androidTestImplementation "com.android.support.test:runner:${rootProject.versions.test_runner}"
}

static def makeKeys(BaseVariant variant, String... keys) {
    keys.each { key ->
        def buildConfigName = "KEY_$key"
        def buildConfigValue = "\"${variant.applicationId}.$key\""
        variant.buildConfigField("String", buildConfigName, buildConfigValue)
    }
}
